plugins {
    id 'com.android.application'
}

android {
    namespace 'com.termux'
    compileSdk 36
    ndkVersion = '29.0.13113456'
    buildToolsVersion '36.0.0'
    defaultConfig {
        applicationId "com.termux"
        minSdk 24
        targetSdk 28
        versionCode 1
        versionName "1.0"
        resourceConfigurations += ['en', 'xhdpi']
        versionNameSuffix '.wear'
        shaders {
            glslcArgs.addAll(['-c', '-g','-Werror'])
        }
        splits {
            abi {
                enable(true)
                reset()
                include 'armeabi-v7a', 'x86_64'
                universalApk false
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            crunchPngs true
            externalNativeBuild {
                ndkBuild {
                    cFlags '-Wall',
                            '-Wextra',
                            '-Werror',
                            '-Ofast',
                            '-fno-stack-protector',
                            '-Wl,--gc-sections',
                            '-Wl,--strip-all ',
                            '-Wl,--icf=safe',
                            '-fvisibility=hidden',
                            '-fvisibility-inlines-hidden',
//                        '-fsanitize=undefined',
                            '-Rpass=loop-vectorize',
                            '-Rpass=inlining',
                            '-Rpass=loop-unroll',
                            '-Rpass-missed=loop-vectorize',
                            '-Rpass-missed=inlining',
                            '-Rpass-missed=loop-unroll',
                            '-flto',
                            '-fomit-frame-pointer',
                            '-s'
                }
            }
        }
        debug {
            jniDebuggable true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled false
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
        resources {
            excludes += ['/META-INF/**']
        }
    }
    buildFeatures {
        shaders true
    }
    lint {
        abortOnError false
    }
    applicationVariants.configureEach { final variant ->
        variant.outputs.configureEach { final output ->
            final def abi = output.getFilter('ABI')
            outputFileName = new File('nyx_' + variant.buildType.name + '_' + (abi ? abi : 'universal') + '.apk')
        }
    }
}